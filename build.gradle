import com.mattmalec.pterodactyl4j.PteroBuilder
import com.mattmalec.pterodactyl4j.client.entities.PteroClient
import org.mangorage.installer.api.core.Datagen

import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.StandardCopyOption

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = 'https://s01.oss.sonatype.org/content/repositories/releases/'
        }
        maven {
            url = 'https://repo.mattmalec.com/repository/releases'
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
        classpath 'com.google.code.gson:gson:2.10.1'
        classpath 'io.github.realmangorage:InstallerAPI:1.0.4'
        classpath 'com.mattmalec:Pterodactyl4J:2.BETA_140'
    }
}

def projectRootDir = project.projectDir.toPath()
def getVersionCode = {
    def proc = 'git rev-list --count master'.execute()
    proc.waitFor()
    return proc.text.trim()
}

def secrets = new Properties()
file("secrets.properties").withInputStream { secrets.load(it) }


apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven-publish'

group 'org.mangorage'
version "${BOT_VERSION}-c" + getVersionCode()
//version ""

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    api.extendsFrom internal
}


repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.mattmalec.com/repository/releases'
    }
    maven {
        url = 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    maven {
        url = 'https://repo.spring.io/plugins-release/'
    }
}

sourceSets {
    main {
        java {
            srcDir 'java'
        }
        resources {
            srcDir 'resources'
        }
    }
}

def libs = [
        JDA: 'net.dv8tion:JDA:5.0.0-beta.17',
        LAVA_PLAYER     : 'dev.arbjerg:lavaplayer:2.0.2',
        GSON            : 'com.google.code.gson:gson:2.10.1',
        LOG4J_CORE      : 'org.apache.logging.log4j:log4j-core:2.20.0',
        LOG4J_SLF4J_IMPL: 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0',
        GITHUB_API      : 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5',
        SLF4J_SIMPLE    : 'org.slf4j:slf4j-simple:2.0.9',
        REFLECTIONS: 'org.reflections:reflections:0.10.2',
        LUA_JAVA   : 'org.luaj:luaj-jse:3.0.1',
        ASM        : 'org.ow2.asm:asm:9.1',
        ASM_TREE   : 'org.ow2.asm:asm-tree:9.2'
]


ext.globalLibs = libs

println("Current Bot Version: ${version}")

dependencies {
    runtimeOnly(libs.LOG4J_SLF4J_IMPL)

    // Libs for Bot

    implementation(libs.JDA)
    implementation(libs.LAVA_PLAYER)
    implementation(libs.GSON)
    implementation(libs.LOG4J_CORE)
    implementation(libs.GITHUB_API)
    implementation(libs.SLF4J_SIMPLE)
    implementation(libs.REFLECTIONS)
    implementation(libs.LUA_JAVA)
    implementation(libs.ASM)
    implementation(libs.ASM_TREE)

    // Core Libs
    implementation project(path: ':mangobotapi')
    implementation project(path: ':mboteventbus')
    implementation project(path: ':mangoutils')

    testImplementation project(path: ':mangoutils')

    internal project(path: ':mangobotapi')
    internal project(path: ':mboteventbus')
    internal project(path: ':mangoutils')
}

publish {
    group "bot tasks"
}

tasks.register('runBot', JavaExec) {
    group "bot tasks"
    description "Runs the built in Launcher."
    dependsOn build
    mustRunAfter build

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.mangorage.mangobot.LauncherMain'
    workingDir = file('build/run/')
}

tasks.register('runDatagen') {
    group "bot tasks"
    description "Runs the data generator"

    doLast {
        String directory = "%s/src/main/resources/installerdata".formatted(getProjectDir())

        Datagen.generateDependenciesJson(
                "%s/src/main/resources/installerdata".formatted(getProjectDir()),
                configurations.implementation.getDependencies().stream().map {
                    "%s:%s:%s".formatted(
                            it.getGroup(),
                            it.getName(),
                            it.getVersion()
                    )
                }.toList() as List<String>
        )

        Datagen.generateIvySettingsXml(directory, getRepositories().stream().map {
            it.getUrl().toString()
        }.toList() as List<String>)
    }
}

tasks.register('publishAndRestartServer') {
    group "bot tasks"
    description "Publish and restart Bot server"

    dependsOn publish
    mustRunAfter publish

    doLast {
        println("Restarting Server...")
        PteroClient client = PteroBuilder.createClient("https://panel.sodiumhosting.com/", secrets.getProperty("SERVER_TOKEN"));

        var server = client.retrieveServerByIdentifier("f32263f3").execute();
        if (server != null) {
            if (server.isSuspended()) {
                println("Server is suspended, unsuspending...");
                server.start().execute();
            } else {
                server.restart().execute();
                println("Restarted Discord Bot Server.");
            }
        }
    }
}

tasks.register('setupInstaller') {
    group "bot tasks"
    description "Sets up the package.json for the installer"
    doLast {
        println("Copying over package.json")
        Path source = projectRootDir.resolve(Paths.get('installer/package.json'))
        Path destination = projectRootDir.resolve(Paths.get('build/run/installer/package.json'))

        try {
            Files.createDirectories(destination)
            Files.copy(source, destination, StandardCopyOption.REPLACE_EXISTING)
            println "File copied successfully from ${source.toAbsolutePath()} to ${destination.toAbsolutePath()}"
        } catch (IOException e) {
            println "Error copying file: ${e.message}"
            e.printStackTrace()
        }
    }
}

tasks.register('runInstaller', JavaExec) {
    group "bot tasks"
    description "runs the installer to get everything configured"

    dependsOn setupInstaller
    mustRunAfter setupInstaller

    classpath = files('installer/installer-1.0.1.jar')
    mainClass = 'org.mangorage.installer.Installer'
    workingDir = file('build/run/')
}


jar {
    dependsOn runDatagen
    dependsOn configurations.internal
    mustRunAfter runDatagen

    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.internal.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }

    manifest {
        attributes 'Main-Class': 'org.mangorage.mangobot.LauncherMain'
    }

    doLast {
        copy {
            from jar.outputs
            into 'build/run/plugins'
            rename { 'mangobot.jar' }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            pom {
                name = 'MangoBot'
                description = 'A discord bot used for many servers'
                url = 'https://github.com/RealMangorage/MangoBot/'
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/RealMangorage/MangoBot/issues'
                }
                developers {
                    developer {
                        id = 'mangorage'
                        name = 'MangoRage'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            group "io.github.realmangorage"
            credentials {
                username = findProperty("mango.maven.username")
                password = findProperty("mango.maven.password")
            }
        }
    }
}